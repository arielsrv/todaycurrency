plugins {
    id 'java-library'
    id 'jacoco'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.43.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.todaycurrency'
version '1.0-SNAPSHOT'
sourceCompatibility = '11'

application {
    mainClass = 'com.todaycurrency.Program'
}

jar {
    enabled = false
}

shadowJar {
    mergeServiceFiles()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '2.0.3'
    implementation group: 'com.google.inject', name: 'guice', version: '5.1.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.13.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
    implementation 'com.netflix.archaius:archaius-core:0.7.7'
    implementation 'commons-configuration:commons-configuration:1.10'
    implementation 'org.apache.curator:curator-framework:5.4.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.1.3'
    implementation group: 'com.newrelic.agent.java', name: 'newrelic-api', version: '7.11.0'
    testImplementation "org.mockito:mockito-core:4.8.1"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-parameters'
    ]
    options.encoding = 'UTF-8'
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/reports/jacoco")
}

jacocoTestReport { // ./gradlew test jacocoTestReport from CLI
    dependsOn test // tests are required to run before generating the report
    group = "Reporting"
    reports {
        xml.enabled(true) // needed for coverage
        csv.enabled(true)
        html.destination file("${buildDir}/reports/coverage")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/todaycurrency/Program.*',
                    '**/todaycurrency/Application.*',
                    '**/sdk/ObjectMapperProvider.*',
                    '**/sdk/SparkWebServer.*',
                    '**/sdk/ApiApplication.*'
            ])
        }))
    }
}